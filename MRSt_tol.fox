INCLUDE 'COSY' ;
PROCEDURE RUN ;

{Georg Berg and Johan Frenje}
{Symmetric MRSt, GB 10/29/2019}

{VARIABLES FOR SYSTEM***********************************}

VARIABLE B1N 1 5;     {dipole1 B-field index}
VARIABLE B2N 1 5;     {dipole1 B-field index}

VARIABLE B1S1 1 5;   {dipole1 entrance-edge curvature}
VARIABLE B1S2 1 5;   {dipole1 exit-edge curvature}
VARIABLE B2S1 1 5;   {dipole2 entrance-edge curvature}
VARIABLE B2S2 1 5;   {dipole2 exit-edge curvature}

VARIABLE S0 1;       {source-to-quadrupole dist}
VARIABLE S1 1;       {quadrupole-to-octupole dist}
VARIABLE S2 1;       {octupole-to-dipole1 dist}
VARIABLE S3 1;       {dipole1-to-dipole2 dist}
VARIABLE S4 1;       {dipole2-to-FP dist}
VARIABLE S5 1;       {temp}

VARIABLE AQ 1;       {quadrupole field}
VARIABLE AH 1;       {quadrupole field gradient}

VARIABLE BQ 1;       {octupole field}
VARIABLE BH 1;       {octupole field gradient}
VARIABLE BO 1;       {octupole field gradient}


VARIABLE u1 1;       {dipole1 entrance angle}
VARIABLE u2 1;       {dipole1 exit angle}
VARIABLE u3 1;       {dipole2 entrance angle}
VARIABLE u4 1;       {dipole2 exit angle}

VARIABLE B1 1;       {dipole1 B field}
VARIABLE B2 1;       {dipole2 B field}
VARIABLE n1 1;       {dipole1 B-field index}
VARIABLE n2 1;       {dipole2 B-field index}

VARIABLE Theta1 1;   {dipole1 bending angle}
VARIABLE Theta2 1;   {dipole2 bending angle}


VARIABLE R1 1;       {dipole1 bending radius for 14-MeV p}
VARIABLE R2 1;       {dipole2 bending radius for 14-MeV p}
VARIABLE Gap 1;      {dipole1&2 half gap}

VARIABLE DE 1;       {spectrometer energey spread}
VARIABLE DA 1;       {emission angle spread in x}
VARIABLE DB 1;       {emission angle spread in y}

{START VARIABLES FOR RAY DEFINITION*********************}

VARIABLE XX 1;       {Source size in X}
VARIABLE AX 1;       {Half-emission angle in X-Z (bending) plane}
VARIABLE YY 1;       {Source size in Y}
VARIABLE AY 1;       {Half-emission angle in Y-Z (non-bending) plane}

VARIABLE EP 1;       {proton energy}
VARIABLE MRESOL_P1 1;{1st-order resolving power}
VARIABLE HO_RES 1;   {HO energy resolution}
VARIABLE gamma 1;    {used for r5 scaling}
VARIABLE dtime 1;    {time resolution}
VARIABLE time_min 1;  {min. total time}
VARIABLE time_max 1;  {max. total time}

VARIABLE denergy 1;  {energy resolution}

VARIABLE deltaE 1;   {energy variation along FP}
VARIABLE deltaX 1;   {distance along FP}
VARIABLE imageX 1;   {image width along FP}
VARIABLE imageE 1;   {image width along FP}


{START VARIABLES FOR READ RAY***************************}

VARIABLE WV 1;       {HO half-width signal distri at FP}
VARIABLE WVY 1;      {1/2 VERTICAL IMAGE SIZE}  
VARIABLE CENTER 1;   {center of HO signal distri at FP}
VARIABLE NMAX 1;     {max number of iterations}
VARIABLE LC 1;       {Length of central ray}

VARIABLE WVPOL 1;       {POLVAL EQUIVALENT TO WV}

{VARIABLE FOR VELOCITY ******************************}
VARIABLE A_DEUT 1; {DEUTERON MASS IN AMU} 
VARIABLE VELOC 1;
VARIABLE TKIN 1;

{VARIABLES FOR FIT_FP **********************************}

VARIABLE NSTEP 1;

VARIABLE H01 1;     {param. used in fit for d1 entrance-edge curv.}
VARIABLE H02 1;     {param. used in fit for d1 exit-edge curv.}
VARIABLE H03 1;     {param. used in fit for d2 entrance-edge curv.}
VARIABLE H04 1;     {param. used in fit for d2 exit-edge curv.}

VARIABLE G01 1;     {param. used in fit for d1 entrance-edge curv.}
VARIABLE G02 1;     {param. used in fit for d1 exit-edge curv.}
VARIABLE G03 1;     {param. used in fit for d2 entrance-edge curv.}
VARIABLE G04 1;     {param. used in fit for d2 exit-edge curv.}

VARIABLE OBJ 1;
VARIABLE TEMP 1;
VARIABLE RAR 1;     {Resolution using AR}

VARIABLE FP_tilt 1; {Focal plane tilt}

VARIABLE RAYVECIN 1 11000 8;
VARIABLE RAYVECOUT 1 11000 8;

VARIABLE NRAYVEC 1;
VARIABLE R 1;

VARIABLE A 1;

{VARIABLES FOR LINEARIZATION OF UNSREWED FOCAL PLANE}

VARIABLE IENERGY 1;
VARIABLE Z1 1; 
VARIABLE Z2 1;
VARIABLE X1 1;
VARIABLE X2 1;

VARIABLE A_COEFF 1;
VARIABLE B_COEFF 1;
VARIABLE C_COEFF 1;
VARIABLE P_DIST  1;
VARIABLE ISOR 1 20 9; {ISOchron.Results}
VARIABLE FOPR 1 20 9; {FOcal Plane Results}
VARIABLE RESDEF 12 20;

VARIABLE M 1;
VARIABLE N 1;
VARIABLE K 1;

{Resulting design parameters}

VARIABLE DESIGN 1 10;
VARIABLE FPDESIGN 1 10; {Focal Plane Design}
VARIABLE ISODESIGN 1 10; {ISO Plane Design}
VARIABLE CRDESIGN 1 5; {Central Ray Design}
VARIABLE DESIGNDEF 18 10;

VARIABLE FLAG_HO 1;

VARIABLE OCT 1;
VARIABLE NN 1;


{RETURNS THE MAXIMUM OF THE Ith COORDINATE OF RAYVEC}
FUNCTION MAXRAYVEC I;
	VARIABLE ITRAY 1;

	MAXRAYVEC:=-1E10;
	LOOP ITRAY 2 NRAYVEC-1;
		IF RAYVECOUT(ITRAY,I)>MAXRAYVEC;
			MAXRAYVEC:=RAYVECOUT(ITRAY,I);
		ENDIF;
	ENDLOOP;
ENDFUNCTION;


{RETURNS THE MINIMUM OF THE Ith COORDINATE OF RAYVEC}
FUNCTION MINRAYVEC I;
	VARIABLE ITRAY 1;
	MINRAYVEC:=1E10;
	LOOP ITRAY 2 NRAYVEC-1;
		IF RAYVECOUT(ITRAY,I)<MINRAYVEC;
			MINRAYVEC:=RAYVECOUT(ITRAY,I);
		ENDIF;
	ENDLOOP;
ENDFUNCTION;


{ set VELOC equal to }
PROCEDURE VELOCITY;
	A_DEUT := 2.013553213;
	VELOC := SQRT(1-1/(1+TKIN/(AMUMEV*A_DEUT))^2)*CLIGHT;
ENDPROCEDURE;


{Focal Plane Curve R, a neq 0 deg, Fp not perpendicular *******************************}
PROCEDURE FPC;
	WRITE 6 'ME(1,6)= ' ME(1,6);
	WRITE 6 'ME(1,1)= ' ME(1,1); 
	WRITE 6 'ME(1,26)= ' ME(1,26);
	WRITE 6 'ME(1,266)= ' ME(1,266);
	WRITE 6 'ME(1,66)= ' ME(1,66);
	WRITE 6 'ME(2,26)= ' ME(2,26);

	R := (ME(1,6)^2+ME(1,1)^2*ME(1,26)^2)^1.5;
	R := R/(ME(1,1)*(ME(1,6)*ME(1,266)-ME(1,26)*(ME(1,66)+2*ME(1,6)*ME(1,1)*ME(2,26))));

	WRITE 6 'Focal plane curvature radius' R;
	FPDESIGN(2):= R;
ENDPROCEDURE;


{MRSt geometrical Layout *******************************}
PROCEDURE MRSt;
	B1S1(1) := TAN(u1*3.14159265359/180);
	B1S2(1) := TAN(u2*3.14159265359/180);
	B2S1(1) := TAN(u3*3.14159265359/180);
	B2S2(1) := TAN(u4*3.14159265359/180);

	B1S1(2) := H01;
	B1S2(2) := H02;
	B2S1(2) := H03;
	B2S2(2) := H04;

	B1S1(3) := G01;
	B1S2(3) := G02;
	B2S1(3) := G03;
	B2S2(3) := G04;

	PS 0.001 ;
	DL S0+(<<aperture_distance>>)+(<<shiftz_H1>>)-(<<length_H1>>/2);
	
	SA -(<<shiftx_H1>>) -(<<shifty_H1>>);
	TA -(<<tiltx_H1>>) -(<<tilty_H1>>);
	M5 0.3+(<<length_H1>>) AQ*<<strength_Q1>> AH*<<strength_H1>> 0.0 0.0 0.0 0.03;
	TA (<<tiltx_H1>>) (<<tilty_H1>>);
	SA (<<shiftx_H1>>) (<<shifty_H1>>);
	
	DL S1-(<<shiftz_H1>>)-(<<length_H1>>/2);
	
	MC R1 Theta1 0.03 B1N B1S1 B1S2 5;

	DL S2/2-0.30+(<<shiftz_O>>)-(<<length_O>>/2);
	
	SA -(<<shiftx_O>>) -(<<shifty_O>>);
	TA -(<<tiltx_O>>) -(<<tilty_O>>);
	M5 0.2+(<<length_O>>) 0 0 OCT*<<strength_O>> 0.0 0.0 0.11;
	TA (<<tiltx_O>>) (<<tilty_O>>);
	SA (<<shiftx_O>>) (<<shifty_O>>);
	
	DL S2/2+0.10-(<<shiftz_O>>)-(<<length_O>>/2);

	CB;
	MC R2 Theta2 0.03 B2N B2S1 B2S2 5;
	CB;
	
	DL S3+(<<shiftz_H2>>)-(<<length_H2>>/2);
	
	SA -(<<shiftx_H2>>) -(<<shifty_H2>>);
	TA -(<<tiltx_H2>>) -(<<tilty_H2>>);
	M5 0.5+(<<length_H2>>) BQ*<<strength_Q2>> BH*<<strength_H2>> 0.0 0.0 0.0 0.20;
	TA (<<tiltx_H2>>) (<<tilty_H2>>);
	SA (<<shiftx_H2>>) (<<shifty_H2>>);
	
	DL 0.60-(<<shiftz_H2>>)-(<<length_H2>>/2);

	WRITE 6 'GEOM R12' ME(1,2);

	PS 0.02;

	WRITE 6 '111111111 S4='&SF(S4,'(F16.5)');
	WRITE 6 '111111111 LC='&SF(LC,'(F16.5)');
	WRITE 6 '111111111 SPOS'&SF(SPOS,'(F16.5)');

	LC := SPOS;
	{WRITE 6 'SPOS=Length Central Ray' SPOS;}
	WRITE 6 'ME(1,1)=' ME(1,1);
	WRITE 6 'ME(1,2)=' ME(1,2);
	WRITE 6 'ME(5,1)=' ME(5,1);
	WRITE 6 'ME(5,2)=' ME(5,2);
	MRESOL_P1 := ABS(ME(1,6))/ABS(2*XX*ME(1,1));
	CRDESIGN(1) := MRESOL_P1;
	WRITE 6 'CRDESIGN(1)' CRDESIGN(1);

	WRITE 6 '1st-order resolving power' MRESOL_P1;
	dtime := 1e12*WVPOL*(1+gamma)/(CONS(v0)*gamma);
	WRITE 6 'dt (ps)' dtime;
	WRITE 6 'dL (mm)' 1e-9*dtime*CONS(v0);
	WRITE 6 'FP Tilt/deg' atan(-ME(1,26)/ME(2,2)/ME(1,6))*180/PI;

	FPC;

	R := ME(1,6)^2/(ME(1,1)*ME(1,266));

	WRITE 6 'Focal plane curvature radius' R;
ENDPROCEDURE;


{DEFINE CHARACTERISTIC RAYS *************************}
PROCEDURE CHARAY;
	VARIABLE IT_RAY 1;
	VARIABLE IT_COORD 1;
	VARIABLE TMP 1;

	{Define 5 target/spectr. entrance parameters}
	XX := 0.0001;
	AX := 1.0*0.00016667; {+/- 0.1 mrad = 0.2 mrad}
	YY := 0.0003;
	AY := 2*5.0*0.00016667; {+/- 1 mrad = 2 mrad}

	VELOCITY;

	WRITE 6 'HERE 3';
	WRITE 6 'DE=' DE;

	TKIN := 12.45*(1+(IENERGY-2)*DE);
	VELOCITY;
	WRITE 6 'TKIN' TKIN;
	WRITE 6 'VELOC' VELOC;
	WRITE 6 'CONS(v0)' CONS(v0);

	CR {Clears all rays};
	UM;

	IF FLAG_HO=0;
		SR 0 0 0 0 0 DE 0 0 1;
		WRITE 6 'FLAG_HO' FLAG_HO;
	ENDIF;

	IF FLAG_HO>0;
		SR 0 AX 0 0 0 DE 0 0 3;
		SR 0 0.5*AX 0 0 0 DE 0 0 3;
		SR 0 0 0 0 0 DE 0 0 3;
		SR 0 -0.5*AX 0 0 0 DE 0 0 3;
		SR 0 -AX 0 0 0 DE 0 0 3;

		SR XX 0 0 0 0 DE 0 0 3;
		SR 0.5*XX 0 0 0 0 DE 0 0 3;
		SR -0.5*XX 0 0 0 0 DE 0 0 3;
		SR -XX 0 0 0 0 DE 0 0 3;

		SR 0 0 0 AY 0 DE 0 0 3;
		SR 0 0 0 0.5*AY 0 DE 0 0 3;
		SR 0 0 0 -0.5*AY 0 DE 0 0 3;
		SR 0 0 0 -AY 0 DE 0 0 3;

		SR 0 0 YY 0 0 DE 0 0 3;
		SR 0 0 0.5*YY 0 0 DE 0 0 3;
		SR 0 0 -0.5*YY 0 0 DE 0 0 3;
		SR 0 0 YY 0 0 DE 0 0 3;

		SR 0 0 YY -AY 0 DE 0 0 3; 
		SR 0 0 -YY -AY 0 DE 0 0 3;

		SR XX/sqrt(2.0) AX YY/sqrt(2.0) AY 0 DE 0 0 3;
		SR XX/sqrt(2.0) AX YY/sqrt(2.0) -AY 0 DE 0 0 3;
		SR XX/sqrt(2.0) AX -YY/sqrt(2.0) AY 0 DE 0 0 3;
		SR XX/sqrt(2.0) -AX YY/sqrt(2.0) AY 0 DE 0 0 3;
		SR -XX/sqrt(2.0) AX YY/sqrt(2.0) AY 0 DE 0 0 3;
		SR XX/sqrt(2.0) AX -YY/sqrt(2.0) -AY 0 DE 0 0 3;
		SR XX/sqrt(2.0) -AX -YY/sqrt(2.0) AY 0 DE 0 0 3;
		SR -XX/sqrt(2.0) -AX YY/sqrt(2.0) AY 0 DE 0 0 3;
		SR -XX/sqrt(2.0) -AX YY/sqrt(2.0) AY 0 DE 0 0 3;
		SR -XX/sqrt(2.0) AX -YY/sqrt(2.0) AY 0 DE 0 0 3;
		SR -XX/sqrt(2.0) AX YY/sqrt(2.0) -AY 0 DE 0 0 3;
		SR XX/sqrt(2.0) -AX YY/sqrt(2.0) -AY 0 DE 0 0 3;
		SR XX/sqrt(2.0) -AX -YY/sqrt(2.0) -AY 0 DE 0 0 3;
		SR -XX/sqrt(2.0) -AX -YY/sqrt(2.0) AY 0 DE 0 0 3;
		SR -XX/sqrt(2.0) -AX YY/sqrt(2.0) -AY 0 DE 0 0 3;
		SR -XX/sqrt(2.0) AX -YY/sqrt(2.0) -AY 0 DE 0 0 3;
		SR -XX/sqrt(2.0) -AX -YY/sqrt(2.0) -AY 0 DE 0 0 3;
	ENDIF;

	NRAYVEC:=1;
	LOOP IT_RAY 1 NRAY;
		LOOP IT_COORD 1 8;
			VELGET RAY(IT_COORD) IT_RAY TMP; RAYVECIN(NRAYVEC,IT_COORD):=TMP;
		ENDLOOP;
		NRAYVEC:=NRAYVEC+1;
	ENDLOOP;
ENDPROCEDURE;


{PROCEDURE READ_RAY ***************************}
PROCEDURE READ_RAY;
	VARIABLE ITRAY 1;
	VARIABLE ITCOORD 1;
	VARIABLE POSI 1 8;
	VARIABLE POSF 1 8;
	VARIABLE SAVEMAP 10000 8;

	SM SAVEMAP;
	CHARAY;
	AM SAVEMAP;

	LOOP ITRAY 1 NRAYVEC-1;
		LOOP ITCOORD 1 8;
			POSI(ITCOORD):=RAYVECIN(ITRAY,ITCOORD);
		ENDLOOP;
		POLVAL 1 SAVEMAP TWOND POSI TWOND POSF TWOND ;
		LOOP ITCOORD 1 8;
			RAYVECOUT(ITRAY,ITCOORD):=POSF(ITCOORD);
		ENDLOOP;
	ENDLOOP; 
ENDPROCEDURE;


PROCEDURE WRITER5VSINPUT II;
	VARIABLE OUTPUT 8000;
	VARIABLE TMP 1;
	VARIABLE TMP2 1;
	VARIABLE ITRAY 1;
	VARIABLE ITCOORD 1;

	LOOP ITRAY 1 NRAYVEC-1;
		OUTPUT:='';
		LOOP ITCOORD 1 6;
			TMP:=RAYVECIN(ITRAY,ITCOORD);
			IF ITCOORD#5;
				OUTPUT:=OUTPUT&S(TMP)&' , ';
			ENDIF;
		ENDLOOP;

		TMP2:=RAYVECOUT(ITRAY,5);
		TMP:= 1e12*RAYVECOUT(ITRAY,5)*(2+cons(ETA))/(CONS(v0)*(1+cons(ETA)));
		OUTPUT:=OUTPUT&S(TMP2)&' , '&S(TMP);
		WRITE II OUTPUT;
	ENDLOOP;
ENDPROCEDURE;


{PROCEDURE FIT_FP *****************************}
PROCEDURE FIT_FP;
	H01 := B1S1(2);
	H02 := B1S2(2);
	H03 := B2S1(2);
	H04 := B2S2(2);

	G01 := B1S1(3);
	G02 := B1S2(3);
	G03 := B2S1(3);
	G04 := B2S2(3);

	u1 := 180*ATAN(B1S1(1))/3.14159265359;
	u2 := 180*ATAN(B1S2(1))/3.14159265359;
	u3 := 180*ATAN(B2S1(1))/3.14159265359;
	u4 := 180*ATAN(B2S2(1))/3.14159265359;

	NSTEP := 0;

	FIT u1 u3;
		CR;
		CHARAY;

		WRITE 6 '1111111111111111111111After CHARAY DE' DE;

		MRST;

		WRITE 6 '222222222222222222222After MRST DE' DE;

		READ_RAY;

		WRITE 6 '3333333333333333333333After READ_RAY DE' DE;

		WV:=(MAXRAYVEC(1)-MINRAYVEC(1))/2;
		WVY:=(MAXRAYVEC(3)-MINRAYVEC(3))/2;

		AR MAP XX AX YY AY DE 0.000 100 RAR;
		MRESOL_P1 := ABS(ME(1,6))/ABS(2*XX*ME(1,1));
		gamma := 1 + CONS(ETA);
		WVPOL:=(MAXRAYVEC(5)-MINRAYVEC(5));

		WRITE 6 'WVPOL' WVPOL;

		WRITE 6 'gamma' gamma;
		WRITE 6 'CONS(v0)' CONS(v0);
		WRITE 6 'RAYVECOUT(2,1)' RAYVECOUT(2,1);
		WRITE 6 'RAYVECOUT(2,2)' RAYVECOUT(2,2);

		time_min:= 1E12*MINRAYVEC(5)*(1+gamma)/(CONS(V0)*gamma);
		time_max:= 1E12*MaxRAYVEC(5)*(1+gamma)/(CONS(V0)*gamma);
		WRITE 6 'min. time/ps' time_min;
		WRITE 6 'max. time/ps' time_max;
		WRITE 6 'max - min_time' time_max-time_min;

		WRITE 6 'RAY(5)' RAY(5);
		WRITE 6 'RAYVECOUT(1,5)' RAYVECOUT(1,5);
		WRITE 6 'RAYVECOUT(2,5)' RAYVECOUT(2,5);
		WRITE 6 'RAYVECOUT(3,5)' RAYVECOUT(3,5);
		WRITE 6 'MAXRAYVEC(5)' MAXRAYVEC(5);
		WRITE 6 'MINRAYVEC(5)' MINRAYVEC(5);

		WRITE 6 '444444444444444444444 Between RAY(1) and RAY(3) DE' DE;

		WRITE 6 'RAY(3)' RAY(3);
		WRITE 6 'RAYVECOUT(1,3)' RAYVECOUT(1,3);
		WRITE 6 'RAYVECOUT(2,3)' RAYVECOUT(2,3);
		WRITE 6 'RAYVECOUT(3,3)' RAYVECOUT(3,3);
		WRITE 6 'MAXRAYVEC(3)' MAXRAYVEC(3);
		WRITE 6 'MINRAYVEC(3)' MINRAYVEC(3);
		WRITE 6 'WVY=Full-Y-SIZE' 2*WVY;

		dtime := 1e12*WVPOL*(1+gamma)/(CONS(v0)*gamma);
		denergy := ABS(EP*1000/MRESOL_P1);
		FP_tilt := atan(-ME(1,26)/ME(2,2)/ME(1,6))*180/PI;
		FPDESIGN(1) := FP_tilt;

		OBJ := ABS(ME(1,2))+ABS(ME(3,4));

		WRITE 6 'dtime' ABS(dtime);
		WRITE 6 'denergy' ABS(denergy);
		WRITE 6 'ME(1,2)' ABS(ME(1,2));
		WRITE 6 'OBJ =' OBJ;
		NSTEP := NSTEP+1;
		WRITE 6 'FIT_FP ITERATION STEP' NSTEP;
	ENDFIT 1e-10 0{0} 1{2}{3} OBJ;

	B1S1(2) := H01;
	B1S2(2) := H02;
	B2S1(2) := H03;
	B2S2(2) := H04;

	B1S1(3) := G01;
	B1S2(3) := G02;
	B2S1(3) := G03;
	B2S2(3) := G04;

	WRITE 6 'B1S1(2)' H01;
	WRITE 6 'B1S2(2)' H02;
	WRITE 6 'B2S1(2)' H03;
	WRITE 6 'B2S2(2)' H04;

	WRITE 6 'B1S1(3)' G01;
	WRITE 6 'B1S2(3)' G02;
	WRITE 6 'B2S1(3)' G03;
	WRITE 6 'B2S2(3)' G04;
ENDPROCEDURE;


{PROCEDURE DE_ALONG ************************}
PROCEDURE DE_ALONG;
	FLAG_HO := 1;  {Central rays only within acceptances}
	FIT_FP;
ENDPROCEDURE;


{MAIN ROUTINE ************************************}
OV 3 3 3 ;
WSET 1.0E-3; 
EP := 12.45;
RP EP 2.00001 1;       {E/MeV, Mass A, Charge z}

WRITE 6 '222222222 S4='&SF(S4,'(F16.5)');
WRITE 6 '222222222 LC='&SF(LC,'(F16.5)');
WRITE 6 '222222222 SPOS'&SF(SPOS,'(F16.5)');

OCT := -0.0045995;

{DEFINITIONS OF RESULTS ************************}
RESDEF(1) := 'DE          ';
RESDEF(2) := 'DE(MeV)     ';
RESDEF(3) := 'V(m/s)      ';
RESDEF(4) := 'dPath(m)    ';
RESDEF(5) := 'Path(m)     ';
RESDEF(6) := 'TOF(ps)     ';
RESDEF(7) := 'dTOF(ps)    ';
RESDEF(8) := 'z*(beam dir)';
RESDEF(9) := 'Angle a(rad)';
RESDEF(10) := 'z=z*cos(a)  ';
RESDEF(11) := 'x(m)in FP   ';
RESDEF(12) := 'x(m)in ISO  ';

{DEFINITION OF DESIGN PARAMETERS ***********************}
DESIGNDEF(1) := 'Tilt Angle(deg)       ';
DESIGNDEF(2) := 'Curv.Radius(m)       ';
DESIGNDEF(3) := 'p-dist(mm)         ';
DESIGNDEF(4) := 'HO Resol.RAY(keV)       ';
DESIGNDEF(5) := 'Time Resol.(ps)       ';
DESIGNDEF(6) := 'y-Size(mm)         ';
DESIGNDEF(7) := 'Plane Length(m)         ';

UM;

FR 3;

FLAG_HO := 0;

S0 := 6.0;
AQ := -0.006467475958002640;
AH := -0.001294895765625000+0.0006;

S1 := 0.3;

u1 := 22.7;
Theta1 := 64;
n1 := 0.0;
u2 := 31.2;

S2 := 1.6;

u3 := 32.4;
Theta2 := 64;
n2 := 0.0;

u4 := 18;

S3 := 0.4;

BQ := -0.041211176;

BH := 0.003;
S4 := 0.3 + 0.3; {Overwritten in Geormetry e.g. 0.49!}

R1 := 0.6;
R2 := 0.6;

B1 := 3.343583719e-8*CONS(v0)/(1.60217662*R1);
B2 := 3.343583719e-8*CONS(v0)/(1.60217662*R2);

B1S1(1) := TAN(u1*3.14159265359/180);
B1S2(1) := TAN(u2*3.14159265359/180);
B2S1(1) := TAN(u3*3.14159265359/180);
B2S2(1) := TAN(u4*3.14159265359/180);

B1S1(2) := 0;
B1S2(2) := 0;
B2S1(2) := 0.05;
B2S2(2) := 0.16;

B1S1(3) := 0.0;
B1S2(3) := 0.0;
B2S1(3) := 0;
B2S2(3) := 0;

B1N(1) := n1;
B2N(1) := n2;

WRITE 6 '333333333 S4='&SF(S4,'(F16.5)');
WRITE 6 '333333333 LC='&SF(LC,'(F16.5)');
WRITE 6 '333333333 SPOS'&SF(SPOS,'(F16.5)');

{ENERGY LOOP *********************}
WRITE 6 'E_LOOP_START*****************************';

LOOP IENERGY 1 9;
	WRITE 6 'IENERGY' IENERGY;
	DE := 0.14; {defines 9 energies, +/- 0.xxx}

	ISOR(1,IENERGY) := (-DE)+(IENERGY-1)*DE/4;
	TKIN := 12.45*(1+((-DE)+(IENERGY-1)*DE/4));
	ISOR(2,IENERGY) := TKIN;
	VELOCITY;
	ISOR(3,IENERGY) := VELOC;

	WRITE 6 'TKIN' TKIN;
	WRITE 6 'VELOC' VELOC;

	DE:= (-DE)+(IENERGY-1)*DE/4;

	WRITE 6 '5555555555555555555555 Before FIT_FP DE' DE;
	FIT_FP;
	WRITE 6 '666666666666666666666 After FIT_ DE' DE;

	WRITE 6 'RAY(1) After 666666' RAY(1);

	IF IENERGY=2;
		PM 6;
		PM 12;

		WRITE 6 'TKIN = ' TKIN;
		WRITE 6 'R12' ME(1,2);
		WRITE 6 'R34' ME(3,4);
		WRITE 6 'S4' S4;
	ENDIF;

	WRITE 6 'MAXRAYVEC(5)' MAXRAYVEC(5);
	WRITE 6 'MINRAYVEC(5)' MINRAYVEC(5);
	ISOR(4,IENERGY) := (MAXRAYVEC(5)+MINRAYVEC(5))/2;
	WRITE 6 'ISOR(4,IENERGY)' ISOR(4,IENERGY);
	WRITE 6 'IENERGY' IENERGY;
	WRITE 6 'RAY(5)' RAY(5);

	ISOR(9,IENERGY) := RAYVECOUT(2,2);
	ISOR(11,IENERGY) := RAYVECOUT(2,1);

	WRITE 6 'IENERGY' IENERGY;
	WRITE 6 'RAYVECOUT(2,2)' RAYVECOUT(2,2);
	WRITE 6 'RAYVECOUT(2,1)' RAYVECOUT(2,1);
	WRITE 6 'E_LOOP_END*****************************'; 
ENDLOOP;

{Now run only central energy with rays within acceptance ************}

DE := 0; {vary DE within E range +/- 0.143}
S4 := S4;

DE_ALONG;

WRITE 6 '77777777777777777777777 After DE_ALONG DE' DE;
{End central energy only ********************}

{Calculate y-size = 2*WVY for central energ DE = 0.143 set} 
DE_ALONG;
ISODESIGN(6) := 2*WVY*1000;
FPDESIGN(6) := 2*WVY*1000;

CR; 
UM; 
CHARAY;

MRSt;

FPDESIGN(4) := EP*1000/(ABS(ME(1,6))/(2*WV));
FPDESIGN(5) := dtime;

WRITE 6 'S0 =' S0;
WRITE 6 'S1 =' S1;
WRITE 6 'S2 =' S2;
WRITE 6 'S3 =' S3;
WRITE 6 'S4 =' S4;

WRITE 6 'R1 =' R1;
WRITE 6 'R2 =' R2;
WRITE 6 'Theta1 =' Theta1;
WRITE 6 'Theta2 =' Theta2;

WRITE 6 'u1 =' u1;
WRITE 6 'u2 =' u2;
WRITE 6 'u3 =' u3;
WRITE 6 'u4 =' u4;
WRITE 6 'B1 =' B1;
WRITE 6 'B2 =' B2;

WRITE 6 'AQ =' AQ;
WRITE 6 'AH =' AH;
WRITE 6 'BQ =' BQ;
WRITE 6 'BH =' BH;

WRITE 6 'H01 =' H01;
WRITE 6 'H02 =' H02;
WRITE 6 'H03 =' H03;
WRITE 6 'H04 =' H04;

WRITE 6 'G01 =' G01;
WRITE 6 'G02 =' G02;
WRITE 6 'G03 =' G03;
WRITE 6 'G04 =' G04;

WRITE 6 'EP (keV) =' 1e3*EP;

deltaE := 1e3*EP*DE;
WRITE 6 'DE (keV) =' deltaE;

deltaX := 1e3*ME(1,6)*DE;   {ME(1,6) given in meters; ratio of the dev. (E - EP)/EP}
WRITE 6 'dX (mm) =' deltaX;

WRITE 6 'dE/dX (keV/mm) =' deltaE/deltaX;

imageX := 1e3*ME(1,1)*XX;
WRITE 6 'Image width (mm) =' imageX;

imageE := imageX*deltaE/deltaX;
WRITE 6 'Image width (keV) =' imageE;

WRITE 6 'ME(5,2) =' ME(5,2);

MRESOL_P1 := ABS(ME(1,6))/ABS(2*XX*ME(1,1));
R := ME(1,6)^2/(ME(1,1)*ME(1,266));

WRITE 6 '1st-order resolving power' abs(MRESOL_P1);
WRITE 6 '1st-order resolution (keV)' abs(EP*1000/MRESOL_P1);
CRDESIGN(2) := ABS(EP*1000/MRESOL_P1);

WRITE 6 'Half-image width, WV (mm)' 1e3*WV;
WRITE 6 'Vert. Half-image width, WVY (mm)' 1e3*WVY;

WRITE 6 'HO resolving power using RAY' ABS(ME(1,6))/(2*WV);
WRITE 6 'HO resolving power using AR' RAR;
WRITE 6 'HO resolution using AR (keV)' EP*1000/RAR;
WRITE 6 'HO resolution using RAY (keV)' EP*1000/(ABS(ME(1,6))/(2*WV));

dtime := 1e12*WVPOL*(1+gamma)/(CONS(v0)*gamma);
WRITE 6 'Time difference (ps)' dtime;
WRITE 6 'Path-length difference (mm)' 1e-9*dtime*CONS(v0);
WRITE 6 'FP Tilt/deg' atan(-ME(1,26)/ME(2,2)/ME(1,6))*180/PI;
WRITE 6 'Focal plane curvature radius' R;
WRITE 6 'ME(1,266)' ME(1,266);

LC := SPOS;

WRITE 6 'SPOS=Length Central Ray' SPOS;

WRITE 6 '444444444 S4='&SF(S4,'(F16.5)');
WRITE 6 '444444444 LC='&SF(LC,'(F16.5)');
WRITE 6 '444444444 SPOS'&SF(SPOS,'(F16.5)');

WRITE 6  'S4' S4;
WRITE 6 'SPOS=Length Central Ray' SPOS;

ISOR(5,5) := LC;
ISOR(5,1) := LC-ISOR(4,1);
ISOR(5,2) := LC-ISOR(4,2);
ISOR(5,3) := LC-ISOR(4,3);
ISOR(5,4) := LC-ISOR(4,4);
ISOR(5,6) := LC-ISOR(4,6);
ISOR(5,7) := LC-ISOR(4,7);
ISOR(5,8) := LC-ISOR(4,8);
ISOR(5,9) := LC-ISOR(4,9);

write 6 'isor(5,3)' isor(5,3);
write 6 'isor(1,3)' isor(1,3);

write 6 'isor(5,1)' isor(5,1);
write 6 'isor(1,1)' isor(1,1);

LOOP K 1 9;
	ISOR(6,K) := 1E12*ISOR(5,K)/ISOR(3,K);
ENDLOOP;

LOOP K 1 9;
	ISOR(7,K) := ISOR(6,K) -ISOR(6,5);
ENDLOOP;

LOOP K 1 9;
	ISOR(8,K) := -ISOR(7,K)*ISOR(3,K)/1E12;
ENDLOOP;

LOOP K 1 9;
	ISOR(10,K):=ISOR(8,K)*COS(ISOR(9,K));
ENDLOOP;

LOOP K 1 9;
	ISOR(12,K):=ISOR(11,K)+ISOR(8,K)*SIN(ISOR(9,K));
ENDLOOP;

WRITE 6 'Time Central Ray (ps)' 1e12*SPOS/CONS(v0);

LOOP M 1 12;
	LOOP N 1 {3}9;
		WRITE 6 'M N'&SF(M,'(I2.1)')&SF(N,'(I2.1)')&' ISOR '&RESDEF(M)&SF(ISOR(M,N),'(F16.5)');
	ENDLOOP;
ENDLOOP;

{***************Set identical ISOR and FOPR parameters}
N := 2;
LOOP M 1 12;
	FOPR(M,N) := ISOR(M,N);
ENDLOOP;

FOPR(1,1) := ISOR(1,1);
FOPR(1,3) := ISOR(1,3);
FOPR(2,1) := ISOR(2,1);
FOPR(2,3) := ISOR(2,3);
FOPR(3,1) := ISOR(3,1);
FOPR(3,3) := ISOR(3,3);

{FP length FPDESIGN(7) calculated as follows *************}
FOPR(12,1) := ME(1,6)*-0.143;
FOPR(12,3) := ME(1,6)*0.143;
FOPR(10,1) := FOPR(12,1)*TAN(FPDESIGN(1)*PI/180);
FOPR(10,3) := FOPR(12,3)*TAN(FPDESIGN(1)*PI/180);
FPDESIGN(7) := SQRT((FOPR(10,1)-FOPR(10,3))^2+(FOPR(12,1)-FOPR(12,3))^2);

WRITE 6 '***************************';

LOOP M 1 12;
	LOOP N 1 3;
		WRITE 6 'M N'&SF(M,'(I2.1)')&SF(N,'(I2.1)')&' FOPR '&RESDEF(M)&SF(FOPR(M,N),'(F16.5)');
	ENDLOOP;
ENDLOOP;

Z1 := ISOR(10,1);
Z2 := ISOR(10,3);
X1 := ISOR(12,1);
X2 := ISOR(12,3);
WRITE 6 'Z1' Z1;
WRITE 6 'Z2' Z2;
WRITE 6 'X1' X1;
WRITE 6 'X2' X2;

WRITE 6 '*****************************'; 
A_COEFF := X2-X1;
B_COEFF := Z2-Z1;
C_COEFF := A_COEFF*Z1-B_COEFF*X1;
P_DIST := +C_COEFF/SQRT(A_COEFF^2+B_COEFF^2);
DESIGN(4) := P_DIST;
ISODESIGN(3) := 1000*P_DIST;
WRITE 6 'A,B,C' A_COEFF B_COEFF C_COEFF;
ISODESIGN(1) := 90+ATAN(-A_COEFF/B_COEFF)*180/PI;

ISODESIGN(4) := FPDESIGN(4);
ISODESIGN(5) := FPDESIGN(5);

WRITE 6 'CENTRAL ENERGY (MeV) = 12.45';
WRITE 6 'CRDESIGN(1)' CRDESIGN(1);

WRITE 6 'N'&SF(1,'(I2.1)')&' CRDESIGN '&'Resolving Power'&SF(CRDESIGN(1),'(F16.5)');
WRITE 6 'N'&SF(2,'(I2.1)')&' CRDESIGN '&'Resolution(keV)'&SF(CRDESIGN(2),'(F16.5)');

{Calc. p-dist with temporary fixed length of FP = 0.67375 m}

FPDESIGN(3) := 1000*(FPDESIGN(2)- SQRT(FPDESIGN(2)*FPDESIGN(2)-0.25*FPDESIGN(7)^2));
IF FPDESIGN(2)<0; 
	FPDESIGN(3) := -1000*(FPDESIGN(3)/1000-2*FPDESIGN(2)); 
ENDIF;

ISODESIGN(7) := SQRT((ISOR(10,1)-ISOR(10,3))^2+(ISOR(12,1)-ISOR(12,3))^2);
ISODESIGN(2) := ISODESIGN(7)^2/(8*ISODESIGN(3)/1000)+ISODESIGN(3)/2000;

WRITE 6 '*****************************'; 

LOOP N 1 7;
	WRITE 6 'N'&SF(N,'(I2.1)')&' FPDESIGN '&DESIGNDEF(N)&SF(FPDESIGN(N),'(F16.5)');
ENDLOOP;
WRITE 6 '*****************************'; 
LOOP N 1 7;
	WRITE 6 'N'&SF(N,'(I2.1)')&' ISODESIGN '&DESIGNDEF(N)&SF(ISODESIGN(N),'(F16.5)');
ENDLOOP;

WRITE 6 'Start FP calculations*******************************';

UM;
DE := 0; {Change DE(%) to calc **** FP params}
S4 := 0.600;

FLAG_HO := 1;  {Central rays only within acceptances}

NSTEP := 0;
FIT S4;
	CR;
	CHARAY;

	BP;
	MRSt;
	EP;

	READ_RAY;
	WRITE 6 'RAY(1) After READ_RAY' RAY(1);
	WRITE 6 'MAXRAYVEC(1)' MAXRAYVEC(1);
	WRITE 6 'MINRAYVEC(1)' MINRAYVEC(1);

	WV := (MAXRAYVEC(1)-MINRAYVEC(1))/2;
	WRITE 6 'HALF X-SIZE' WV;
	NSTEP := NSTEP+1;
	WRITE 6 'Fit Iteration Step' NSTEP;
ENDFIT 1E-11 0 1 WV;
WRITE 6 'S4:=' S4;

{
PG -1 -2; {on GRAPHICS screen}
PP -10 0. 0.; {pic001.ps}
PP -10 0. 90.; {pic002.ps}
}

WRITE 6 RAY(3);
WRITE 6 'MAXRAYVEC(3)' MAXRAYVEC(3);
WRITE 6 'MINRAYVEC(3)' MINRAYVEC(3);

WVY := (MAXRAYVEC(3)-MINRAYVEC(3));
WRITE 6 '****Full-Y-SIZE/mm' WVY*1000;

WRITE 6 'RAY(5)' RAY(5);
WRITE 6 'MAXRAYVEC(5)' MAXRAYVEC(5);
WRITE 6 'MINRAYVEC(5)' MINRAYVEC(5);

gamma := 1 + CONS(ETA);
WVPOL:=(MAXRAYVEC(5)-MINRAYVEC(5));

WRITE 6 'WVPOL' WVPOL;

WRITE 6 'gamma' gamma;
WRITE 6 'CONS(v0)' CONS(v0);
dtime := 1e12*WVPOL*(1+gamma)/(CONS(v0)*gamma);
WRITE 6 ' ' ;
WRITE 6 '********************' ;
WRITE 6 '****DE' DE;
WRITE 6 '****DE/MeV' TKIN;
WRITE 6 '****S4:=' S4;
WRITE 6 '****z/m:=' (S4-0.6);
WRITE 6 '****x/m =' (MAXRAYVEC(1)+MINRAYVEC(1))/2;
WRITE 6 '****HO Resol. by RAY(keV)' EP*1000/(ABS(ME(1,6))/(2*WV));
WRITE 6 '****dtime' ABS(dtime);
WRITE 6 '****Full-Y-SIZE/mm' WVY*1000;
WRITE 6 '********************' ;
WRITE 6 ' ' ;

WRITE 6 'dx/mm:=' 1000*ABS(MAXRAYVEC(1)-MINRAYVEC(1));
WRITE 6 'FP Tilt/deg' atan(-ME(1,26)/ME(2,2)/ME(1,6))*180/PI;
WRITE 6 'FP Tilt/rad' atan(-ME(1,26)/ME(2,2)/ME(1,6));
WRITE 6 'FP COS(Tilt/rad)' COS(atan(-ME(1,26)/ME(2,2)/ME(1,6)));

WRITE 6 'dz/mm:=' 1000*ABS(MAXRAYVEC(1)-MINRAYVEC(1))/COS(atan(-ME(1,26)/ME(2,2)/ME(1,6)));

WRITE 6 'R12' ME(1,2);
WRITE 6 'R34' ME(3,4);

WRITE 11 'ISOR(1,N) ISOR(2,N)   ISOR(3,N)   ISOR(4,N)  ISOR(5,N)   ISOR(6,N)     ISOR(7,N)
   ISOR(8,N) ISOR(9,N) ISOR(10,N)  ISOR(11,N)  ISOR(12,N)';
WRITE 11 '   DE       E/MeV        v(m/s)    d_path/m    path/m    TOF/ps        dTOF/ps
      z^prime    a/rad      z/m        x/m     x/m@IsoPlane';
LOOP M 1 9;
	M := 10-M;
	WRITE 11 ''&SF(ISOR(1,M),'(F7.3)')&SF(ISOR(2,M),'(F10.5)')&SF(ISOR(3,M),'(F16.5)')&SF(ISOR(4,M),
	'(F10.5)')&SF(ISOR(5,M),'(F10.5)')&SF(ISOR(6,M),'(F14.5)')&SF(ISOR(7,M),'(F14.5)')&SF(ISOR(8,M),
	'(F10.5)')&SF(ISOR(9,M),'(F10.5)')&SF(ISOR(10,M),'(F12.5)')&SF(ISOR(11,M),'(F11.5)')&SF(ISOR(12,M),
	'(F12.5)');
ENDLOOP;
WRITE 11 'TILT ANGLE' FPDESIGN(1);

FPC;

PM 6;

ENDPROCEDURE;
RUN ;
END ;