INCLUDE 'COSY';

PROCEDURE run;

	PROCEDURE set_rays;
		VARIABLE a 1;
		VARIABLE b 1;

		CR; {clear rays}
		LOOP a -.01 .01 .01;
			LOOP b -.01 .01 .01;
				SR 0 a 0 b 0 0 0 0 5; {set the rays}
			ENDLOOP;
		ENDLOOP;
		SB 0 .01 0 0 .01 0 0 .01 0 0 0; {set the beam dimensions}
	ENDPROCEDURE;

	PROCEDURE define_geometry face_angle;
		VARIABLE field_coefs 1 1;
		VARIABLE shape_1 1 1;
		VARIABLE shape_2 1 1;
		
		field_coefs(1) := 0;
		shape_1(1) := face_angle;
		shape_2(1) := face_angle;

		DL .50; {drift of length 50 cm}
		MC .10 30 .01 field_coefs shape_1 shape_2 1; {magnetic dipole component, 10 cm radius, 30 degrees, 1 cm aperture}
		DL .50; { drift of length 50 cm}
		PS .01; {poincare section (but it's actually just a line)}
	ENDPROCEDURE;

	PROCEDURE optimize_system;
		VARIABLE face_angle 1;
		VARIABLE error 1;

		OV 3 3 0; {3rd order, 3d phase space, 0 parameters}
		RP 15 1 1; {kinetic energy 15MeV, mass 1 Da, charge 1 e}
		FR 3; {fringe field mode 3 (enge function)}
		
		FIT face_angle;
		
			UM; {set the map to unity}
			
			set_rays;
			
			PTY 10; {set picture type to true-scale}
			BP; {begin picture}

			define_geometry face_angle;

			EP; {end picture}
			PG -1 -2; {show the picture from the x and y directions with an envelope (windows 1 and 2)}
			
			error := ME(1,2)^2;
			
			WRITE 6 face_angle;
		
		ENDFIT 1e-5 1000 1 error;
		
		PM 6; {prints the map to the console}
	ENDPROCEDURE;
	
	optimize_system;

ENDPROCEDURE;

run;

END;
